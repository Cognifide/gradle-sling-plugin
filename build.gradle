plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'com.jfrog.bintray' version '1.8.4'
}

group = 'com.cognifide.gradle'
version = '4.1.1'
description = 'Gradle Sling Plugin'
defaultTasks = ['clean', 'build', 'publishToMavenLocal']

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.2.51"
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.2.51'
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'commons-io:commons-io:2.4'
    implementation 'commons-validator:commons-validator:1.6'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.8.8'
    implementation 'org.reflections:reflections:0.9.9'
    implementation 'org.apache.jackrabbit.vault:vault-cli:3.1.24'
    implementation 'org.jsoup:jsoup:1.10.3'
    implementation 'org.samba.jcifs:jcifs:1.3.18-kohsuke-1'
    implementation 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.0.0'
    implementation 'org.zeroturnaround:zt-zip:1.11'
    implementation 'com.hierynomus:sshj:0.21.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.4'
    implementation 'org.apache.httpcomponents:httpmime:4.5.4'
    implementation 'org.osgi:org.osgi.core:6.0.0'
    implementation 'com.mitchellbosecke:pebble:2.4.0'
    implementation 'fr.jcgay.send-notification:send-notification:0.14.0'
    implementation 'com.dorkbox:Notify:3.7'

    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
}

gradlePlugin {
    plugins {
        base {
            id = "com.cognifide.sling.base"
            implementationClass = "com.cognifide.gradle.sling.base.BasePlugin"
        }
        pkg {
            id = "com.cognifide.sling.package"
            implementationClass = "com.cognifide.gradle.sling.pkg.PackagePlugin"
        }
        bundle {
            id = "com.cognifide.sling.bundle"
            implementationClass = "com.cognifide.gradle.sling.bundle.BundlePlugin"
        }
        instance {
            id = "com.cognifide.sling.instance"
            implementationClass = "com.cognifide.gradle.sling.instance.InstancePlugin"
        }
    }
}

processResources {
    doLast {
        file("$buildDir/resources/main/build.json") << groovy.json.JsonOutput.toJson([
                pluginVersion: project.version,
                gradleVersion: project.gradle.gradleVersion
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

test {
    testLogging {
        events "failed"
        exceptionFormat "short"
    }
}

test.dependsOn publishToMavenLocal
check.dependsOn publishToMavenLocal

bintray {
    user = project.properties.get('bintray.user') ?: System.getenv('BINTRAY_USER')
    key = project.properties.get('bintray.key') ?: System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven-public'
        name = 'gradle-sling-plugin'
        userOrg = 'cognifide'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Cognifide/gradle-sling-plugin.git'
        labels = ['sling', 'vault', 'scr']
        version {
            name = "${project.version}"
            desc = "${project.description} ${project.version}"
            vcsTag = "gsp-${project.version}"
        }
    }
    publish = project.properties["bintray.publish"] ?: true
    dryRun = project.properties["bintray.dryRun"] ?: false
    override = project.properties["bintray.override"] ?: false
}